plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'signing'
}

def srcDirs = [
    'com/google/android/material/animation',
    'com/google/android/material/appbar',
    'com/google/android/material/badge',
    'com/google/android/material/behavior',
    'com/google/android/material/bottomappbar',
    'com/google/android/material/bottomnavigation',
    'com/google/android/material/bottomsheet',
    'com/google/android/material/button',
    'com/google/android/material/canvas',
    'com/google/android/material/card',
    'com/google/android/material/checkbox',
    'com/google/android/material/chip',
    'com/google/android/material/circularreveal',
    'com/google/android/material/circularreveal/cardview',
    'com/google/android/material/circularreveal/coordinatorlayout',
    'com/google/android/material/color',
    'com/google/android/material/datepicker',
    'com/google/android/material/dialog',
    'com/google/android/material/drawable',
    'com/google/android/material/elevation',
    'com/google/android/material/expandable',
    'com/google/android/material/floatingactionbutton',
    'com/google/android/material/imageview',
    'com/google/android/material/internal',
    'com/google/android/material/math',
    'com/google/android/material/menu',
    'com/google/android/material/navigation',
    'com/google/android/material/navigationrail',
    'com/google/android/material/progressindicator',
    'com/google/android/material/radiobutton',
    'com/google/android/material/resources',
    'com/google/android/material/ripple',
    'com/google/android/material/shape',
    'com/google/android/material/shadow',
    'com/google/android/material/slider',
    'com/google/android/material/snackbar',
    'com/google/android/material/stateful',
    'com/google/android/material/switchmaterial',
    'com/google/android/material/tabs',
    'com/google/android/material/textfield',
    'com/google/android/material/textview',
    'com/google/android/material/theme',
    'com/google/android/material/theme/overlay',
    'com/google/android/material/timepicker',
    'com/google/android/material/tooltip',
    'com/google/android/material/transition',
    'com/google/android/material/transformation',
    'com/google/android/material/typography',
]

android {
    compileSdk target_sdk

    defaultConfig {
        minSdk 23
        targetSdk target_sdk
        versionCode 1
        versionName sesl_material_version
    }

    sourceSets {
        main.manifest.srcFile 'java/com/google/android/material/AndroidManifest.xml'

        main.resources.srcDir "$buildDir/javaResources"

        main.java.srcDir 'java'
        main.java.includes = srcDirs.collect { it + '/**/*.java' }
        main.java.excludes = [
            '**/build/**',
        ]

        srcDirs.forEach {
            main.res.srcDirs += 'java/' + it + '/res'
            main.res.srcDirs += 'java/' + it + '/res-public'
        }
    }

    buildTypes.all {
        consumerProguardFiles 'proguard-behaviors.pro', 'proguard-inflater.pro'
    }

    libraryVariants.all {
        it.processJavaResourcesProvider.get().dependsOn(writeVersionFile)
    }
}

dependencies {
    api("androidx.annotation:annotation:$androidx_annotation_version")
    api project(path: ':appcompat')
    api("androidx.cardview:cardview:$androidx_cardview_version")
    api project(path: ':coordinatorlayout')
    api("androidx.constraintlayout:constraintlayout:$androidx_constraintlayout_version")
    api project(path: ':core')
    api("androidx.dynamicanimation:dynamicanimation:$androidx_dynamicanimation_version")
    api("androidx.annotation:annotation-experimental:$androidx_annotation_experimental_version")
    api project(path: ':fragment')
    api("androidx.lifecycle:lifecycle-runtime:$androidx_lifecycle_version")
    api project(path: ':recyclerview')
    api("androidx.transition:transition:$androidx_transition_version")
    api("androidx.vectordrawable:vectordrawable:$androidx_vectordrawable_version")
    api project(path: ':viewpager2')
}

task writeVersionFile() {
    def versionFileDir = "$buildDir/javaResources/META-INF"
    def versionFileName = "com.google.android.material_material.version"

    new File(versionFileDir).mkdirs()
    new File(versionFileDir + '/' + versionFileName).text = "1.0.43-sesl4" + "\n"
}

ext {
    POM_NAME = "Sesl Material Components"
    POM_DESCRIPTION = "Sesl Material Components Library."
}
